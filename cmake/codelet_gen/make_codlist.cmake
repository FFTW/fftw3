include(${CMAKE_SOURCE_DIR}/cmake/argument_macros.cmake)

function(fftw_gen_codlist)
  set(_keywords "XRENAME" "SOLVTAB" "HEADER" "CODELETS" "OUTPUT_FILE")
  unset(_lastkeyword)
  unset(_codlistgen_xrename)
  unset(_codlistgen_solvtab)
  unset(_codlistgen_xtralines)
  unset(_codlistgen_codelets)
  unset(_codlistgen_outfile)
  foreach(arg IN LISTS ARGN)
    if(arg IN_LIST _keywords)
      set(_lastkeyword ${arg})
      continue()
    elseif(_lastkeyword STREQUAL "XRENAME")
      arg_check_and_set(_codlistgen_xrename ${arg} XRENAME)
    elseif(_lastkeyword STREQUAL "SOLVTAB")
      arg_check_and_set(_codlistgen_solvtab ${arg} SOLVTAB)
    elseif(_lastkeyword STREQUAL "HEADER")
      list(APPEND _codlistgen_xtralines ${arg})
    elseif(_lastkeyword STREQUAL "CODELETS")
      list(APPEND _codlistgen_codelets ${arg})
    elseif(_lastkeyword STREQUAL "OUTPUT_FILE")
      arg_check_and_set(_codlistgen_outfile ${arg} OUTPUT_FILE)
    endif()
  endforeach()
  arg_require_set(_codlistgen_solvtab SOLVTAB)
  arg_require_set(_codlistgen_xrename XRENAME)
  arg_require_set(_codlistgen_codelets CODELETS)
  arg_require_set(_codlistgen_outfile OUTPUT_FILE)

  unset(_codlistgen_filecontent)

  file(READ ${FFTW_copyright_header} _codlist_copyright_txt)
  string(APPEND _codlistgen_filecontent "${_codlist_copyright_txt}\n" )
  string(TIMESTAMP _codlistgen_timestamp "%Y-%m-%d %H:%MZ" UTC)
  string(APPEND _codlistgen_filecontent "/* Generated automatically on ${_codlistgen_timestamp} */\n")
  string(APPEND _codlistgen_filecontent "/*  -->  DO NOT EDIT!  <-- */\n\n")
  string(APPEND _codlistgen_filecontent "#include \"ifftw.h\"\n\n")
  foreach(_codlist_xline IN LISTS _codlistgen_xtralines)
    string(APPEND _codlistgen_filecontent "${_codlist_xline}\n")
  endforeach()

  foreach(_codlist_codelet IN LISTS _codlistgen_codelets)
    get_filename_component(_codlist_codelet_name ${_codlist_codelet} NAME_WE)
    string(APPEND _codlistgen_filecontent "extern void ${_codlistgen_xrename}(codelet_${_codlist_codelet_name})(planner *);\n")
  endforeach()
  string(APPEND _codlistgen_filecontent "\n\n")
  string(APPEND _codlistgen_filecontent "extern const solvtab ${_codlistgen_solvtab};\n")
  string(APPEND _codlistgen_filecontent "const solvtab ${_codlistgen_solvtab} = {\n")
  foreach(_codlist_codelet IN LISTS _codlistgen_codelets)
    get_filename_component(_codlist_codelet_name ${_codlist_codelet} NAME_WE)
    string(APPEND _codlistgen_filecontent "   SOLVTAB(${_codlistgen_xrename}(codelet_${_codlist_codelet_name})),\n")
  endforeach()
  string(APPEND _codlistgen_filecontent "   SOLVTAB_END\n};\n")
  file(GENERATE OUTPUT ${_codlistgen_outfile} CONTENT "${_codlistgen_filecontent}")
endfunction()
